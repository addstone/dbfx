import java.text.SimpleDateFormat

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'com.gluonhq.client-gradle-plugin' version '0.1.33'
}

mainClassName = 'cn.navigational.dbfx.Launcher'

//jvm args
applicationDefaultJvmArgs = ['-Dsun.java2d.opengl=true', '-XX:+UseZGC', '-Xms512M', '-Xmx1024M']
if (javafx.platform.classifier == 'linux') applicationDefaultJvmArgs.add("-Djdk.gtk.version=2")

javafx {
    version = "15"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.base', 'javafx.graphics', 'javafx.swing']
}


jar {
    from {
        configurations.runtime.collect {
            if (!it.name.startsWith("javafx")) {
                zipTree(it)
            }
        }
    }
    archiveFileName = "dbfx.jar"
    manifest {
        attributes 'Multi-Release': true
        attributes 'Main-Class': mainClassName
        attributes 'App-Name': rootProject.name
        attributes "App-Version": project.version
        attributes 'App-Author': '752544765@qq.com'
        attributes 'App-Website': 'http://navigational.cn'
        attributes 'Build-Time': new SimpleDateFormat().format(new Date())
        attributes 'Copyright': 'Copyright Â© 2019-2020 navigational.cn'
    }
}

dependencies {
    compile project(":kit")
    compile group: 'commons-codec', name: 'commons-codec', version: '1.14'
    compile group: 'org.controlsfx', name: 'controlsfx', version: '11.0.2'
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.5'
    compile group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.13'
}

